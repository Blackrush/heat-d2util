allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    group = 'org.heat.dofus'
    version = '0.6-SNAPSHOT'

	apply from: rootDir.absolutePath + "/gradle/remote.gradle"

    ext {
        lombok = '1.14.0'
        junit = '4.11'
        heatUtil = '0.4.+'
        heatD2vendor = '2.21.3-9'
        netty = '4.0.+'
        slf4j = '1.7.7'
        logback = '1.1.2'
    }

    configurations {
        provided
    }

    dependencies {
        provided "org.projectlombok:lombok:$lombok"

        testCompile "junit:junit:$junit"
    }

    sourceSets {
        main { compileClasspath += [configurations.provided] }
        test { compileClasspath += [configurations.provided] }
    }

    idea { module { scopes.PROVIDED.plus += [configurations.provided] } }
}

dependencies {
    compile "org.heat:util:$heatUtil"
    compile "org.heat:util-io:$heatUtil"
    compile "io.netty:netty-buffer:$netty"

    testCompile "org.heat:util-testing:$heatUtil"
    testCompile "org.slf4j:slf4j-api:$slf4j"
    testCompile("org.heat.dofus:d2vendor:$heatD2vendor") {
        exclude group: "io.netty"
    }
    testRuntime "ch.qos.logback:logback-classic:$logback"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task docs(type: Javadoc) {
    source         = files(allprojects*.sourceSets*.main*.allJava)
    classpath      = files(allprojects*.sourceSets*.main*.compileClasspath)
    destinationDir = file("${buildDir}/docs/docs")
}

